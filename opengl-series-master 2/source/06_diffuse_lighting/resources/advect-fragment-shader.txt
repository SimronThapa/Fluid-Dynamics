#version 150

uniform sampler2D OldVelocity;
uniform float DeltaT;
uniform vec2 InverseGridSize;

in vec2 fragTexCoord;

out vec4 finalColor;

void main(void)
{
    // Simple advection by backstep.
    vec2 samplePos = fragTexCoord - texture(OldVelocity, fragTexCoord).xy * DeltaT * InverseGridSize;
    vec2 outputVelocity = texture(OldVelocity, samplePos).xy;

    // Special force: Flow from left to right
    if(fragTexCoord.x < 0.05 && fragTexCoord.y < 0.7 && fragTexCoord.y > 0.3)
        outputVelocity.x += (0.05 - fragTexCoord.x) * 10000.0 * DeltaT;

    if(fragTexCoord.x > 1.0 - InverseGridSize.x * 2.0)
        outputVelocity = vec2(0.0, 0.0);

    //obstacle
    vec2 toCenter = vec2(0.2, 0.5) - fragTexCoord;
    toCenter.x *= InverseGridSize.y / InverseGridSize.x;
    if(dot(toCenter, toCenter) < 0.002)
        outputVelocity = vec2(0.0, 0.0);

    finalColor = vec4(outputVelocity, 0.0, 0.0);
}

